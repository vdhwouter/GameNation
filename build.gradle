buildscript {
    ext.project_name = 'GameNation'
    ext.project_group = 'org.gamenation'
    ext.project_artifact = 'gamenation'
    ext.project_version = '1.0'

    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.2"
}

group project_group
version project_version


allprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'kotlin'
}

publishing {
    publications {
        publish(MavenPublication) {
            from components.java
            groupId project_group
            artifactId project_artifact
            version project_version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['publish']
    pkg {
        repo = 'maven'
        name = project_name
        licenses = ['Unlicense']
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/vdhwouter/$project_name"
        issueTrackerUrl = "https://github.com/vdhwouter/$project_name/issues"
        vcsUrl = "https://github.com/vdhwouter/${project_name}.git"
        githubRepo = "vdhwouter/$project_name"
        version {
            name = project_version
            vcsTag = project_version
            gpg {
                sign = true
            }
        }

    }
}





repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
